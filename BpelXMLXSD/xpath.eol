operation xpath(expr : String) : String {
  	var result : String = expr;
  	if (expr.matches("concat\\(.*\\)")) {
  		result = evalConcat(expr);
  	} else if (expr.matches("count\\(.*\\)")) {
  		result = evalCount(expr);  	
  	} else if (expr.startsWith("$")) {
  		result = evalXpathReference(expr);
  	}
  	return result;
}

// concat(p1,p2)
operation evalConcat(expr : String) : String {
	var lrValue = expr.split(",");
	var par1 : String = lrValue.first().split("\\(").second();
	return xpath(par1) + " + " + xpath(lrValue.second());
}

// count(expr)
operation evalCount(expr : String) : String {
	var result = "Utils.getVarValueInt(\"" + makePath(trimXpathPrefix(trimAllNs(replaceSlash(expr)))) +  "\", varMap)";
	return result;
}

//$path
//operation evalXpathReference(expr : String) : String {
//	var lrValue = expr.split("\\.");
//	var ref : String = lrValue.first().toLowercase(); 
//	return ref.substring(1);
//}

operation evalXpathReference(expr : String) : String {
  	var result : String = "";
	var tokens = expr.split("/");
	for(t in tokens) {
		var sep = "";
		if (result.length() > 0) {
			sep = ".";
		}
		result = result + sep + trimNs(t);
	}
	
	var theType = getReturnType(trimIndex(result));
	result = createReturnValue(theType, makePath(trimXpathPrefix(trimAllNs(replaceSlash(result)))));
	return result;
}

operation trimXpathPrefix(s : String) : String {
	var result = s;
	if(result.startsWith("$")) {
		result = s.substring(1);
	} else {
		result = s.replace("\\$","");
	}
	return result;
}

operation trimIndex(s : String) : String {
	return s.split("\\[").first();
}

operation getReturnType(javaPath : String) : String {
	var theType = getTypes().getType(getLeaf(javaPath)); 
	if (isPrimitiveXmlType(theType)) {
		theType = tranlateXmlPrimitiveTypesToJava(theType);
	}
	return theType;
}

operation createReturnValue(theType : String, xpath : String) : String {
	var result = "(" + theType + ") "; 
	if ("String".equals(theType)) {
		result = result + "Utils.getVarValue(";
	} else if ("int".equals(theType)) {
		result = result + "Utils.getVarValueInt(";
	} else {
		result = result + "Utils.getVarValueO(";
	}
	result = result + "\"" + xpath + "\", varMap)";
	return result;
}
